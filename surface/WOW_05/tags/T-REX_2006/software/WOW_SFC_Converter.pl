#! /usr/bin/perl -w

package WOW_SFC_Converter;

##Module-------------------------------------------------------------------
# <p>The WOW_SFC_Converter is a script for converting the Desert Research
# Institute (WOW) surface data from its source netCDF format to the QCF format.
#  This conversion only generates the 5 minutes resolution QCF format file.  It
# does not generate a source frequency or special record files.</p>
#
# @author Joel Clawson
# @version TREX_2006 This conversion was originally developed for T-REX.  It
#    uses code written by Janine Goldstein for the reading on NetCDF data used
#    in the MADIS surface conversion.
##Module-------------------------------------------------------------------
use strict;
use lib "/work/software/TREX/library/conversion_modules/Version6";
use lib "/net/work/software/TREX/library/conversion_modules/Version6";
use DpgCalculations;
use DpgDate;
use RecordMap;
use Station::SimpleStationMap;
use Station::Station;
use Surface::QCFConstants;
use Surface::QCFSurfaceRecord;

$! = 1;

&main();

##-------------------------------------------------------------------------
# @signature void main()
# <p>Execute the script to run the conversion.</p>
##-------------------------------------------------------------------------
sub main {
    my $converter = WOW_SFC_Converter->new();
    $converter->convert();
    $converter->clean_empty_files();
}

##-------------------------------------------------------------------------
# @signature WOW_SFC_Converter new()
# <p>Create a new instance of the ASU_SFC_Converter class.  This defines
# the class and the constants used by the conversion.</p>
##-------------------------------------------------------------------------
sub new {
    my $invocant = shift;
    my $class = $invocant || ref($invocant);
    my $self = {};
    bless($self,$class);

    $self->{"stations"} = Station::SimpleStationMap->new();
    $self->{"records"} = RecordMap->new();
    $self->{"record_times"} = {};

    $self->{"NETWORK"} = "WOW05";
    $self->{"PROJECT"} = "T-REX";

    $self->{"START_TOI"} = "2006/03/01";
    $self->{"END_TOI"} = "2006/04/30";

    $self->{"OUT_DIR"} = "../output";
    $self->{"FINAL_DIR"} = "../final";
    $self->{"RAW_DIR"} = "../raw_data";

    $self->{"DUPES_FILE"} = sprintf("%s/dupes.log",$self->{"OUT_DIR"});
    $self->{"SEQUENCE_FILE"} = sprintf("%s/sequence.log",$self->{"OUT_DIR"});
    $self->{"STN_SUM_FILE"} = sprintf("%s/station_summary.log",$self->{"OUT_DIR"});
    $self->{"warning_file"} = sprintf("%s/warning.log",$self->{"OUT_DIR"});

    return $self;
}

##-------------------------------------------------------------------------
# @signature void check_for_duplicates()
# <p>Search the records generated by the conversion to see if duplicate
# records were found and print them to a duplicate log file.</p>
##-------------------------------------------------------------------------
sub check_for_duplicates {
    my ($self) = @_;
    
    open(my $DUPES,sprintf(">%s",$self->{"DUPES_FILE"})) or die("Can't create dupes file.\n");
    foreach my $dupe ($self->{"records"}->getDuplicateRecords()) {
        my $rec = $self->{"records"}->getRecord($dupe->getStationId(),$dupe->getNetworkId(),$dupe->getNominalDate(),$dupe->getNominalTime());

        if ($rec->toQCF_String(0) eq $dupe->toQCF_String(0)) {
            printf($DUPES "Exact duplicate found at %s in %s at %s %s.\n",$rec->getStationId(),$rec->getNetworkId(),$rec->getNominalDate(),$rec->getNominalTime());
        } else {
            printf($DUPES "Duplicate records found: %s %s at %s %s.  Keeping the first record.\n\t%s\t%s",$rec->getStationId(),$rec->getNetworkId(),$rec->getNominalDate(),$rec->getNominalTime(),$rec->toQCF_String(0),$dupe->toQCF_String(0));
        }
    }
    close($DUPES);
}

##-------------------------------------------------------------------------
# @signature void check_for_sequence_problems()
# <p>Check the records generated by the conversion to see if any of the
# expected records are missing.  The messages are placed into a sequence
# log file.</p>
##-------------------------------------------------------------------------
sub check_for_sequence_problems {
    my ($self) = @_;
    
    open (my $SEQ,sprintf(">%s",$self->{"SEQUENCE_FILE"})) or die("Can't create the sequence file.\n");
    print($SEQ $self->{"records"}->check5minuteSequence($self->{"START_TOI"},$self->{"END_TOI"}));
    close($SEQ);
}

##-------------------------------------------------------------------------
# @signature void clean_empty_files()
# <p>Remove all zero length log files and display a message to the user
# that the particular log was not generated.</p>
##-------------------------------------------------------------------------
sub clean_empty_files {
    my ($self) = @_;

    # Remove an empty warning file.
    if (-z $self->{"warning_file"}) {
	printf("There were not any warnings generated for the conversion.\n");
	unlink($self->{"warning_file"});
    }

    # Remove an empty duplicate record file.
    if (-z $self->{"DUPES_FILE"}) {
	printf("There were not any duplicate records found during the conversion.\n");
	unlink($self->{"DUPES_FILE"});
    }

    # Remove an empty sequence file.
    if (-z $self->{"SEQUENCE_FILE"}) {
	printf("There were not any missing records found during the conversion.\n");
	unlink($self->{"SEQUENCE_FILE"});
    }
}

##-------------------------------------------------------------------------
# @signature void convert()
# <p>Perform the conversion of the raw data to the QCF format.  This will
# generate the necessary directories, QCF data files, station lists, and
# other log files.</p>
##-------------------------------------------------------------------------
sub convert {
    my ($self) = @_;

    mkdir($self->{"OUT_DIR"}) unless(-e $self->{"OUT_DIR"});
    mkdir($self->{"FINAL_DIR"}) unless(-e $self->{"FINAL_DIR"});

    open($self->{"WARN"},">".$self->{"warning_file"}) or die("Can't create warning file.\n");

    $self->load_stations();
    $self->read_raw_files();
    $self->generate_output_files();
    $self->generate_station_files();

    close($self->{"WARN"});
}

##-------------------------------------------------------------------------
# @signature String clean_for_file_name(String text)
# <p>Remove/translate characters in a String so it can be used in a file name.</p>
# 
# @input $text The String to be cleaned.
# @output $text The cleaned up String.
##-------------------------------------------------------------------------
sub clean_for_file_name {
    my ($self,$text) = @_;
    
    # Convert spaces to underscores.
    $text =~ s/\s+/_/g;

    # Remove all hyphens
    $text =~ s/\-//g;

    return $text;
}

##-------------------------------------------------------------------------
# @signature void generate_output_files()
# <p>Generate the output files including the data file, duplicate record
# file, sequence problem file, and warning file.</p>
##-------------------------------------------------------------------------
sub generate_output_files {
    my ($self) = @_;

    printf("Generating output files...\n");

    # Check for problems with the records in the record map.
    $self->check_for_duplicates();
    $self->check_for_sequence_problems();

    # Generate the data file.
    my $outfile = sprintf("%s/%s.0qc",$self->{"OUT_DIR"},lc($self->clean_for_file_name($self->{"NETWORK"})));
    open(my $OUT,">$outfile") or die("Can't create output file: $outfile\n");
    foreach my $record ($self->{"records"}->getAllRecords()) {
      # Undo the hack of using the present weather hold the seconds
      $record->setPresentWeather(undef);
        if ($self->in_time_of_interest($record)) {
            my $out = $record->toQCF_String();
            $out =~ s/ \-0\.00 /  0\.00 /g;
            print($OUT $out);
	    $self->{"stations"}->getStation($record->getStationId(),$self->{"NETWORK"})
	        ->insertDate($record->getNominalDate(),"YYYY/MM/DD");
        }
    }
    close($outfile);
}

##-------------------------------------------------------------------------
# @signature void generate_station_files()
# <p>Create the station list and the station summary files.</p>
##-------------------------------------------------------------------------
sub generate_station_files {
    my ($self) = @_;

    my $station_file = sprintf("%s/%s_%s_surface_stationCD.out",$self->{"FINAL_DIR"},
			       $self->clean_for_file_name($self->{"NETWORK"}),
			       $self->clean_for_file_name($self->{"PROJECT"}));
    open(my $STN,">$station_file") or die("Can't create station file: $station_file\n");
    foreach my $station ($self->{"stations"}->getAllStations()) {
	print($STN $station->toString()) unless ($station->getBeginDate() =~ /^9+$/);
    }
    close($STN);

    open(my $SUM,">".$self->{"STN_SUM_FILE"}) or die("Can't create the station summary file.\n");
    print($SUM $self->{"stations"}->getStationSummary());
    close($SUM);
}

##-------------------------------------------------------------------------
# @signature int in_time_of_interest(Record record)
# <p>Determine if the current record is in the time of interest for the 
# current project.</p>
# 
# @input $record The record to be tested to be in the time of interest.
# @return $result <code>1</code> if the record is in the time of interest,
# <code>0</code> otherwise.
##-------------------------------------------------------------------------
sub in_time_of_interest {
    my ($self,$record) = @_;

    return compareDates($self->{"START_TOI"},"YYYY/MM/DD",$record->getNominalDate(),"YYYY/MM/DD") >= 0 && compareDates($record->getNominalDate(),"YYYY/MM/DD",$self->{"END_TOI"},"YYYY/MM/DD") >= 0;
}

##-------------------------------------------------------------------------
# @signature void load_stations()
# <p>Load the stations where the data was collected from the station list.
##-------------------------------------------------------------------------
sub load_stations {
    my ($self) = @_;

    my $station = Station::Station->new("WOW",$self->{"NETWORK"});
    $station->setStationName("Weather Station on Wheels");

    $station->setLatLongAccuracy(4);
    $station->setStateCode("CA");
    $station->setReportingFrequency("1 second");
    $station->setPlatformIdNumber(376);
    $station->setNetworkIdNumber(99);
    $station->setMobilityFlag("m");
    
    $self->{"stations"}->addStation($station);
}

##-------------------------------------------------------------------------
# @signature void parse_raw_file(String file)
# <p>Process the file by parsing the raw data and converting it into the
# QCF format.</p>
#
# @input $file The full path to the file to be parsed.
##-------------------------------------------------------------------------
sub parse_raw_file {
  my ($self,$file) = @_;
  
  printf("Processing file: %s...\n",$file);
  
  open(my $FILE, $file) or die("Can't open $file\n");
  
  foreach my $line (<$FILE>) {
    chomp($line);

    next if ($line =~ /^\%/ || $line =~ /^\s*$/);
    
    my $station = $self->{"stations"}->getStation("WOW",$self->{"NETWORK"});
    
    my @data = split(' ',$line);
    
    if ($data[0] =~ /[05]0[0-4]$/ || $data[0] =~ /[49]5\d$/) {
      my $record = Surface::QCFSurfaceRecord->new($self->{"WARN"},$station);
      $record->setReadingTime(substr($data[0],0,8),"YYYYMMDD",substr($data[0],8,6),"HHMMSS",0);
      
      $record->setLatitude($data[1],"DDDDDDDDDDD") unless ($data[1] =~ /NAN/i);
      $record->setLongitude($data[2], "-DDDDDDDDDDDD") unless ($data[2] =~ /NAN/i);
      
      $record->setWindUComponent($data[6], "m/s") unless ($data[6] =~ /NAN/i);
      $record->setWindVComponent($data[7], "m/s") unless ($data[7] =~ /NAN/i);
      $record->setTemperature($data[8], "C") unless ($data[8] =~ /NAN/i);
      $record->setDewPoint($data[9], "C") unless ($data[9] =~ /NAN/i);
      $record->setPressure($data[14], "hPa") unless ($data[14] =~ /NAN/i);

      # Abuse the present weather to hold the seconds.
      $record->setPresentWeather(substr($data[0],12,2));

      if ($record->getActualTime() =~ /[49]$/) {
	my ($date,$time) = adjustDateTime($record->getActualDate(),"YYYY/MM/DD",$record->getActualTime(),"HH:MM",0,0,1,0);
	$record->setActualDate($date,"YYYY/MM/DD");
	$record->setActualTime($time,"HH:MM");
	$record->setNominalDate($date,"YYYY/MM/DD");
	$record->setNominalTime($time,"HH:MM");
      }

      my $stored = $self->{"records"}->getRecord($record->getStationId(),$record->getNetworkId(),$record->getNominalDate(),$record->getNominalTime());
      if (defined($stored)) {
	my $storedSecond = $stored->getPresentWeather();
	my $newSecond = $record->getPresentWeather();
	
	if ($newSecond == 0 || $newSecond == $storedSecond) {
	  $self->{"records"}->removeRecord($record->getStationId(),$record->getNetworkId(),$record->getNominalDate(),$record->getNominalTime());
	  $self->{"records"}->addRecord($record);
	} elsif ($newSecond >= 50 && $storedSecond >= 50 && $newSecond >= $storedSecond) {
	  $self->{"records"}->removeRecord($record->getStationId(),$record->getNetworkId(),$record->getNominalDate(),$record->getNominalTime());
	  $self->{"records"}->addRecord($record);	  
	} elsif ($newSecond <= 5 && $storedSecond <= 5 && $newSecond <= $storedSecond) {
	  $self->{"records"}->removeRecord($record->getStationId(),$record->getNetworkId(),$record->getNominalDate(),$record->getNominalTime());
	  $self->{"records"}->addRecord($record);
	}

      } else {
	$self->{"records"}->addRecord($record);
      }
    }
  }
}

##-------------------------------------------------------------------------
# @signature void read_raw_files()
# <p>Read in the list of raw data files in the raw data directory and 
# parse them into the QCF format.</p>
##-------------------------------------------------------------------------
sub read_raw_files {
    my ($self) = @_;
    
    opendir(my $RAWDIR,$self->{"RAW_DIR"}) or die("Unable to read raw directory: ".
						  $self->{"RAW_DIR"}."\n");
    my @files = sort(grep(/\.txt$/,readdir($RAWDIR)));
    closedir($RAWDIR);

    foreach my $file (@files) {
	$self->parse_raw_file(sprintf("%s/%s",$self->{"RAW_DIR"},$file));
    }
}

##-------------------------------------------------------------------------
# @signature void update_for_TREX(QCFSurfaceRecord record)
# <p>Update the specified record for changes that only apply during the
# T-REX project.</p>
#
# @input $record The record to be updated.
##-------------------------------------------------------------------------
sub update_for_TREX {
    my ($self,$record) = @_;

}
